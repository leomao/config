# Enable Powerlevel10k instant prompt. Should stay close to the top of ~/.zshrc.
# Initialization code that may require console input (password prompts, [y/n]
# confirmations, etc.) must go above this block; everything else may go below.
if [[ -r "${XDG_CACHE_HOME:-$HOME/.cache}/p10k-instant-prompt-${(%):-%n}.zsh" ]]; then
  source "${XDG_CACHE_HOME:-$HOME/.cache}/p10k-instant-prompt-${(%):-%n}.zsh"
fi

function source_if() {
  [[ ! -r "$1" ]] || source "$1"
}

# prompt theme
source_if "/usr/share/zsh-theme-powerlevel10k/powerlevel10k.zsh-theme"

# To customize prompt, run `p10k configure` or edit ~/.p10k.zsh.
source_if "${HOME}/.p10k.zsh"

source_if "${HOME}/.zsh/vim.zsh/vim.zsh"

#############################
# Load zle
#############################
autoload -Uz url-quote-magic
zle -N self-insert url-quote-magic

#############################
# Options
#############################
# don't record duplicate history
setopt hist_ignore_dups
setopt appendhistory
setopt sharehistory
setopt incappendhistory
HISTSIZE=5000
HISTFILE=~/.zsh_history
SAVEHIST=5000

# no flow control
setopt noflowcontrol

# rm confirmation
setopt rm_star_wait

# Directory Stack settings
DIRSTACKSIZE=8
setopt auto_cd
setopt autopushd pushdminus pushdsilent pushdtohome pushd_ignore_dups

setopt mark_dirs
setopt multios

# also do completion for aliases
setopt complete_aliases

#############################
# Aliases
#############################
# List direcory contents
if (( $+commands[eza] )) ; then
  alias ls='eza --group-directories-first'
  alias l='ls -F'
  alias ll='ls -glF'
  alias la='ll -a'
  alias lx='ll -s extension'
  alias lk='ll -rs size'
  alias lt='ll -ars modified'
elif (( $+commands[lsd] )) ; then
  alias ls='lsd --icon never --group-dirs first'
  alias l='ls -F'
  alias ll='ls -lF'
  alias la='ll -A'
  #alias lx='ll -s extension'
  alias lk='ll -rS'
  alias lt='ll -rt'
else
  alias ls='ls -h --color --group-directories-first'
  alias l='ls -F'
  alias ll='ls -lF'
  alias la='ll -A'
  alias lx='ls -lXB'
  alias lk='ls -lSr'
  alias lt='ls -lAFtr'
fi
alias sl=ls # often screw this up

# grep
if (( $+commands[rg] )); then
  alias gg='rg'
elif (( $+commands[ag] )); then
  alias gg='ag'
else
  alias gg='grep -R -n'
fi

# Show history
alias his='fc -l 1'

# Tmux 256 default
alias tmux='tmux -2'

# vim alias
if [[ `vim --version 2> /dev/null | grep -- +clientserver` ]] ; then
  # always use vim client server
  alias vim='vim --servername vim'
fi
if (( $+commands[nvim] )) ; then
  alias v='nvim'
else
  alias v='vim'
fi

# Directory Stack alias
alias dirs='dirs -v'
alias ds='dirs'

# use thefuck if available
if (( $+commands[thefuck] )) ; then
  eval $(thefuck --alias)
  alias fk='fuck'
fi

# fzf
if (( $+commands[fzf] )) ; then
  source <(fzf --zsh)
  export FZF_DEFAULT_OPTS="-m --cycle"
  if (( $+commands[rg] )) ; then
    export FZF_DEFAULT_COMMAND="rg --files"
  fi
fi

#############################
# Completions
#############################
autoload -Uz compinit
compinit

# Important
zstyle ':completion:*:default' menu yes=long select=2

# Completing Groping
zstyle ':completion:*:options' description 'yes'
zstyle ':completion:*:descriptions' format '%F{226}Completing %F{214}%d%f'
zstyle ':completion:*' group-name ''

# Completing misc
zstyle ':completion:*' matcher-list '' 'm:{[:lower:]}={[:upper:]} r:|[._-]=* r:|=*' 'm:{[:lower:][:upper:]}={[:upper:][:lower:]}'
zstyle ':completion:*' verbose yes
zstyle ':completion:*' completer _expand _complete _match _prefix _approximate _list _history
zstyle ':completion:*' expand prefix suffix
zstyle ':completion:*:*files' ignored-patterns '*?.o' '*?~' '*\#'
zstyle ':completion:*' use-cache true
zstyle ':completion:*:*:-subscript-:*' tag-order indexes parameters
zstyle ':completion:*' select-prompt %SScrolling active: current selection at %p%s

# Directory
zstyle ':completion:*:cd:*' ignore-parents parent pwd
export LS_COLORS='di=1;34:ln=36:so=32:pi=33:ex=32:bd=34;46:cd=34;43:su=30;41:sg=30;46:tw=30;42:ow=30;43'
zstyle ':completion:*' list-colors ${(s.:.)LS_COLORS}

# default: --
zstyle ':completion:*' list-separator '-->'
zstyle ':completion:*:manuals' separate-sections true

# load custom settings
source_if "${HOME}/.zshrc.local"

source_if "/usr/share/zsh/plugins/zsh-autosuggestions/zsh-autosuggestions.zsh"
source_if "/usr/share/zsh/plugins/zsh-syntax-highlighting/zsh-syntax-highlighting.zsh"

unfunction source_if

# vim: ft=zsh
